ExternalEvent.OnGetMoneySample
{
	"\![raiseplugin,c58b6320-caf8-11ed-a901-0800200c9a66,OnWalletOfUnyu,set,1,Thanks for clicking!]\e"
}

OnMouseDoubleClick
{
	if reference5 == "0"
	{
		if WalletInstalled
		{
			"\![raiseplugin,c58b6320-caf8-11ed-a901-0800200c9a66,OnWalletOfUnyu,get]\![raise,OnMenu]"
		}
		else; OnVisitBank
	}
}

OnWalletOfUnyu
{
	balance = TOINT(reference1)
}

OnMenu
{
	"\![set,autoscroll,disable]\_qBalance: %(balance) UNYU\n\n"
	--
	"\![*]\q[Check transaction history,OnTransactionHistory]\n"
	--
	"\![*]\q[Visit bank,OnVisitBank]\n\n"
	--
	"\![*]\q[Cancel,OnCancel]"
}

OnTransactionHistory
{
	"\![raiseplugin,c58b6320-caf8-11ed-a901-0800200c9a66,OnWalletOfUnyu,menu]"
}

OnVisitBank
{
	"\j[https://zichqec.github.io/atm_machine/]"
}

//Gets a list of installed plugins
On_installedplugin
{
    pluginlist = IARRAY
    foreach reference; _ref
    {
        pluginlist ,= _ref[0,C_BYTE1]
    }
}

WalletInstalled
{
    if ASEARCH("Wallet of Unyu",pluginlist) != -1; 1
    else; 0
}

OnTranslate
{
	_talk = reference0
	
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	TOSTR(_talk)
}

On_homeurl
{
	"https://raw.githubusercontent.com/Zichqec/atm_machine/main/"
}